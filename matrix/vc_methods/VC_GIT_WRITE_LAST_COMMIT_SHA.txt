//%attributes = {"lang":"en","shared":true,"executedOnServer":true} comment added and reserved by 4D.
// method gets last commit from "git log" and writes it down in last_imported_commit_sha.git


C_TEXT($filename)
$filename:="last_imported_commit_sha.git"

// let's get the last commit first

C_TEXT($output;$error;$last_commit_sha)

VC_GIT_EXEC_CMD ("log";"--oneline -n1 --format=%h";->$output;->$error)


// if it went wrong with git
If (VC_GIT_OK=0)
ALERT(VC_GIT_ERR_MSG)
$0:=50

Else 
// ok continue
$last_commit_sha:=VC_GIT_LAST_COMMIT_SHA
ALERT("got last commit sha to write : "+$last_commit_sha)

C_TIME($vhDocRef)
If (Test path name($filename)#Is a document)
// document doesn't exist
//create it
$vhDocRef:=Create document($filename)
If (OK=1)
SEND PACKET($vhDocRef;$last_commit_sha)
ALERT("Created document "+$filename+"\n String : \n"+$last_commit_sha)
CLOSE DOCUMENT($vhDocRef)
// document closed and global vc_git_ok set to true
VC_GIT_OK:=1
Else 
// error creating the file
$0:=55
End if 
Else 
// document exists
// open it
$vhDocRef:=Open document($filename;Read and write)
If (OK=1)
// first get file content to prepend
C_TEXT($fileBody;$finalFileBody;$firstLine)
RECEIVE PACKET($vhDocRef;$fileBody;Get document size($filename))

// check if the commit we're about to write is already in file
C_LONGINT($CR_position;$length_CR)
$CR_position:=Position("\n";$fileBody;1;$length_CR)
If ($CR_position=0)
// we know the file is not empty so if carriage return not found 
// take the whole string as first
$firstLine:=$fileBody
$firstLine:=Replace string($firstLine;Char(Line feed);"")
$firstLine:=Replace string($firstLine;Char(Carriage return);"")
Else 
// multiple lines found. taking the first
$firstLine:=Substring($fileBody;1;$CR_position-1)
$firstLine:=Replace string($firstLine;Char(Line feed);"")
$firstLine:=Replace string($firstLine;Char(Carriage return);"")
End if 

If ($last_commit_sha=$firstLine)
// this commit hashcode is already in file skip write
ALERT($last_commit_sha+" commit hashcode is already in file. Skip write")
CLOSE DOCUMENT($vhDocRef)
VC_GIT_OK:=1
Else 
//prepend last commit to write
$finalFileBody:=$last_commit_sha+"\n"+$fileBody
ALERT("Writing to file : "+$filename+"\n String : \n"+$finalFileBody)
// moving the cursor back to the beginning of the file since it was moved by the last RECEIVE PACKET call
SET DOCUMENT POSITION($vhDocRef;1)
SEND PACKET($vhDocRef;$finalFileBody)
CLOSE DOCUMENT($vhDocRef)
// document closed and global vc_git_ok set to true
VC_GIT_OK:=1
End if 


Else 
//error opening the file
$0:=56
CLOSE DOCUMENT($vhDocRef)
End if 
End if 

End if ;